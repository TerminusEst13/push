actor "Push Gun" : Weapon
{
    +WEAPON.NOAUTOAIM
    Weapon.SelectionOrder 1
    Weapon.Slotnumber 2
    States
    {
    NormalReady:
        TNT1 A 0 A_PlayWeaponSound("draw/switch")
        TNT1 A 0 A_TakeInventory("DrawingToolReady",1)
        TNT1 A 0 A_TakeInventory("DrawingToolOn",1)
        RGUN A 5 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
    Ready:
        TNT1 A 0 A_JumpIfInventory("DrawingToolOn",1,"DrawReady")
        RGUN A 1 A_WeaponReady
        Loop
    Deselect:
        TNT1 A 0 A_TakeInventory("DrawingToolReady",1)
        TNT1 A 0 A_TakeInventory("DrawingToolOn",1)
        TNT1 AAAAA 0 A_Lower
        RGUN A 1 A_Lower
        Loop
    Select:
        TNT1 AAAAA 0 A_Raise
        RGUN A 1 A_Raise
        Loop
    Fire:
        TNT1 A 0 A_TakeInventory("PowerRespawnProtection",1)
        TNT1 A 0 A_SetShootable
        TNT1 A 0 A_JumpIfInventory("DrawingToolReady",1,"DrawFire")
        TNT1 A 0 A_JumpIfInventory("WallHackVision",1,"Nope")
        TNT1 A 0 A_PlayWeaponSound("push/fire")
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("LevelIsTooBright",1,"BrightFire")
        TNT1 A 0 A_FireCustomMissile("LaserBullet",0,0,0,8)
        goto FireFinish
    BrightFire:
        TNT1 A 0 A_FireCustomMissile("LaserBulletBright",0,0,0,8)
    FireFinish:
        RGUN B 2 Bright A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        RGUN IICA 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        Goto Ready
    AltFire:
        TNT1 A 0 A_TakeInventory("PowerRespawnProtection",1)
        TNT1 A 0 A_SetShootable
        TNT1 A 0 A_JumpIfInventory("DrawingToolReady",1,"NormalReady")
        TNT1 A 0 A_JumpIfInventory("WallHackVision",1,"Nope")
        RGUN A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        TNT1 A 0 A_PlayWeaponSound("push/altfire")
        TNT1 A 0 A_JumpIfInventory("FakeWaterFlight",1,4)
        TNT1 A 0 A_JumpIfInventory("PowerFlight",1,3)
        TNT1 A 0 A_JumpIfInventory("IBelieveICanFly",1,2)
        TNT1 A 0 A_GiveInventory("SurfingCounter",1)
        TNT1 A 0
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("LevelIsTooBright",1,"BrightAltFire")
        TNT1 A 0 A_FireCustomMissile("PlasmaGrenade",0,0,0,0,0,5)
        goto AltFireFinish
    BrightAltFire:
        TNT1 A 0 A_FireCustomMissile("PlasmaGrenadeBright",0,0,0,0,0,5)
    AltFireFinish:
        RGUN H 2 Bright A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        RGUN D 25 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        RGUN EEFFGA 2 A_WeaponReady(WRF_NOFIRE)
        Goto Ready
    Nope:
        RGUN A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        goto Ready

    Spawn:
        TNT1 A -1
        Stop

    DrawReady:
        TNT1 A 0 A_PlayWeaponSound("draw/switch")
        TNT1 A 0 A_GiveInventory("DrawingToolReady",1)
        PGUN A 5 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
    DrawReady2:
        PGUN A 1 A_WeaponReady
        loop
    DrawFire:
        TNT1 A 0 A_PlayWeaponSound("draw/draw")
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_FireCustomMissile("DrawBullet",0,0,0,8)
        PGUN B 1 Bright A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        TNT1 A 0 A_Refire
        PGUN CDA 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        Goto DrawReady2
    }
}

ACTOR PlasmaGrenade
{
    Radius 12
    Height 8
    Speed 50
    Damage (10)
    Projectile
    -NOGRAVITY
    +SKYEXPLODE
    +THRUGHOST
    +FORCEXYBILLBOARD
    Decal Scorch
    Scale 0.4
    Gravity 0.5
    RenderStyle Add
    DamageType "GrenadeDamage"
    Alpha 0.99
    States
    {
    Spawn:
        PFS1 AB 2 Bright
        Loop
    Death:
    Crash:
        TNT1 A 0
        PFE1 A 1 Bright
        TNT1 A 0 A_PlaySound("push/grenadehit")
        TNT1 A 0 A_NoGravity
        TNT1 A 0 A_JumpIfInTargetInventory("SurfingCounter",15,"Nope")
        TNT1 A 0 A_SpawnitemEx("PushGrenadeSelfJumpage",0,0,0,0,0,0,0,48)
    Nope:
        TNT1 A 0 A_SpawnitemEx("PushGrenadeJumpage",0,0,0,0,0,0,0,48)
        TNT1 AAAAAAAAAA 0 A_SpawnItemEx("LaserHaze3",random(-16,16),random(-16,16),random(-10,10),random(-100,100)/150.0,random(-100,100)/150.0,random(-100,100)/150.0,0,160)
        TNT1 AAAAAAAAAA 0 A_SpawnItemEx("LaserHaze4",random(-16,16),random(-16,16),random(-10,10),random(-100,100)/80.0,random(-100,100)/80.0,random(-100,100)/80.0,0,160)
        PFE1 BCDEF 2 Bright
        Stop
    }
}

actor PlasmaGrenadeBright : PlasmaGrenade
{
    RenderStyle Translucent
    States
    {
    Death:
    Crash:
        TNT1 A 0
        PFE1 A 1 Bright
        TNT1 A 0 A_PlaySound("push/grenadehit")
        TNT1 A 0 A_NoGravity
        TNT1 A 0 A_JumpIfInTargetInventory("SurfingCounter",15,"Nope")
        TNT1 A 0 A_SpawnitemEx("PushGrenadeSelfJumpage",0,0,0,0,0,0,0,48)
    Nope:
        TNT1 A 0 A_SpawnitemEx("PushGrenadeJumpage",0,0,0,0,0,0,0,48)
        TNT1 AAAAAAAAAA 0 A_SpawnItemEx("LaserHaze3Bright",random(-16,16),random(-16,16),random(-10,10),random(-100,100)/150.0,random(-100,100)/150.0,random(-100,100)/150.0,0,160)
        TNT1 AAAAAAAAAA 0 A_SpawnItemEx("LaserHaze4Bright",random(-16,16),random(-16,16),random(-10,10),random(-100,100)/80.0,random(-100,100)/80.0,random(-100,100)/80.0,0,160)
        PFE1 BCDEF 2 Bright
        Stop
    }
}

ACTOR PushGrenadeSelfJumpage
{
    Radius 1
    Height 1
    Speed 0
    +NOCLIP
    +FORCERADIUSDMG
    -SOLID
    +DONTBLAST
    DamageType "NoDamage"
    Obituary "%k shouldn't have killed %o with this."
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Explode(196,176,1)
        Stop
    }
}

ACTOR PushGrenadeJumpage
{
    Radius 1
    Height 1
    Speed 0
    +NOCLIP
    +FORCERADIUSDMG
    -SOLID
    +DONTBLAST
    DamageType "PushDamage2"
    Obituary "%k shouldn't have killed %o with this."
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_RadiusThrust(6400,256,0)
        TNT1 AAA 1 A_Explode(128,256,0) // Launching players is unbelievably hard.
                                        // Normally this nudges them up only by a little,
                                        // but by the end they're pushing under them.
        TNT1 A 1
        Stop
    }
}

actor DrawBullet : FastProjectile
{
    Radius 1
    Height 1
    Speed 300
    Damage (1)
    Decal DrawingChip
    DamageType "NoDamage"
    +THRUGHOST
    Projectile
    States
    {
    Spawn:
       TNT1 AAAAA 1
       TNT1 A 0 A_Stop
       TNT1 A 0 A_Die
       goto Death
    Death:
        TNT1 A 0 //A_UnsetShootable
        TNT1 AAAAA 0 A_SpawnItemEx("LaserHaze5",0,0,0,random(-100,100)/150.0,random(-100,100)/150.0,random(-100,100)/150.0,0,32)
        TNT1 AAAAA 0 A_SpawnItemEx("LaserHaze6",0,0,0,random(-100,100)/80.0,random(-100,100)/80.0,random(-100,100)/80.0,0,32)
        TNT1 A 4
        stop
    }
}

ACTOR LaserBullet : FastProjectile
{
    Radius 4
    Height 4
    Speed 650
    Damage (10)
    Decal HImpScorch
    MissileType "BulletBlasterTrail"
    DamageType "PushDamage"
    ReactionTime 20
    +THRUGHOST
    Projectile
    States
    {
    Spawn:
        TNT1 A 1 /*A_SpawnItemEx("BulletBlasterTrail",(0.01*momx)/-35.0,-(0.01*momy)/-35.0,2+(0.01*momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrail",(0.5*momx)/-35.0,-(0.5*momy)/-35.0,2+(0.5*momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrail", (1 *momx)/-35.0, -(1 *momy)/-35.0, 2+(1 *momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrail",(1.5*momx)/-35.0,-(1.5*momy)/-35.0,2+(1.5*momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrail", (2 *momx)/-35.0, -(2 *momy)/-35.0, 2+(2 *momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrail",(2.5*momx)/-35.0,-(2.5*momy)/-35.0,2+(2.5*momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrail", (3 *momx)/-35.0, -(3 *momy)/-35.0, 2+(3 *momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrail",(3.5*momx)/-35.0,-(3.5*momy)/-35.0,2+(3.5*momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrail", (4 *momx)/-35.0, -(4 *momy)/-35.0, 2+(4 *momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrail",(4.5*momx)/-35.0,-(4.5*momy)/-35.0,2+(4.5*momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrail", (5 *momx)/-35.0, -(5 *momy)/-35.0, 2+(5 *momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrail",(5.5*momx)/-35.0,-(5.5*momy)/-35.0,2+(5.5*momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrail", (6 *momx)/-35.0, -(6 *momy)/-35.0, 2+(6 *momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrail",(6.5*momx)/-35.0,-(6.5*momy)/-35.0,2+(6.5*momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrail", (7 *momx)/-35.0, -(7 *momy)/-35.0, 2+(7 *momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrail",(7.5*momx)/-35.0,-(7.5*momy)/-35.0,2+(7.5*momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrail", (8 *momx)/-35.0, -(8 *momy)/-35.0, 2+(8 *momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)*/
        TNT1 A 0 A_CountDown
        Loop
    Death:
        TNT1 A 0 //A_UnsetShootable
        TNT1 A 0 A_PlaySound("push/laserhit")
        TNT1 A 0 A_SpawnItemEx("PushLaserJumpage",0,0,0,0,0,0,0,48)
        TNT1 AAAAA 0 A_SpawnItemEx("LaserHaze",0,0,0,random(-100,100)/150.0,random(-100,100)/150.0,random(-100,100)/150.0,0,32)
        TNT1 AAAAA 0 A_SpawnItemEx("LaserHaze2",0,0,0,random(-100,100)/80.0,random(-100,100)/80.0,random(-100,100)/80.0,0,32)
        TNT1 A 4
        stop
    }
}

actor LaserBulletBright : LaserBullet
{
    RenderStyle Translucent
    MissileType "BulletBlasterTrailBright"
    States
    {
    Spawn:
        TNT1 A 1 /*A_SpawnItemEx("BulletBlasterTrailBright",(0.01*momx)/-35.0,-(0.01*momy)/-35.0,2+(0.01*momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrailBright",(0.5*momx)/-35.0,-(0.5*momy)/-35.0,2+(0.5*momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrailBright", (1 *momx)/-35.0, -(1 *momy)/-35.0, 2+(1 *momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrailBright",(1.5*momx)/-35.0,-(1.5*momy)/-35.0,2+(1.5*momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrailBright", (2 *momx)/-35.0, -(2 *momy)/-35.0, 2+(2 *momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrailBright",(2.5*momx)/-35.0,-(2.5*momy)/-35.0,2+(2.5*momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrailBright", (3 *momx)/-35.0, -(3 *momy)/-35.0, 2+(3 *momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrailBright",(3.5*momx)/-35.0,-(3.5*momy)/-35.0,2+(3.5*momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrailBright", (4 *momx)/-35.0, -(4 *momy)/-35.0, 2+(4 *momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrailBright",(4.5*momx)/-35.0,-(4.5*momy)/-35.0,2+(4.5*momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrailBright", (5 *momx)/-35.0, -(5 *momy)/-35.0, 2+(5 *momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrailBright",(5.5*momx)/-35.0,-(5.5*momy)/-35.0,2+(5.5*momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrailBright", (6 *momx)/-35.0, -(6 *momy)/-35.0, 2+(6 *momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrailBright",(6.5*momx)/-35.0,-(6.5*momy)/-35.0,2+(6.5*momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrailBright", (7 *momx)/-35.0, -(7 *momy)/-35.0, 2+(7 *momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrailBright",(7.5*momx)/-35.0,-(7.5*momy)/-35.0,2+(7.5*momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("BulletBlasterTrailBright", (8 *momx)/-35.0, -(8 *momy)/-35.0, 2+(8 *momz)/-35.0, 0,0,0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)*/
        TNT1 A 0 A_CountDown
        Loop
    Death:
        TNT1 A 0 //A_UnsetShootable
        TNT1 A 0 A_PlaySound("push/laserhit")
        TNT1 A 0 A_SpawnItemEx("PushLaserJumpage",0,0,0,0,0,0,0,48)
        TNT1 AAAAA 0 A_SpawnItemEx("LaserHazeBright",0,0,0,random(-100,100)/150.0,random(-100,100)/150.0,random(-100,100)/150.0,0,32)
        TNT1 AAAAA 0 A_SpawnItemEx("LaserHaze2Bright",0,0,0,random(-100,100)/80.0,random(-100,100)/80.0,random(-100,100)/80.0,0,32)
        TNT1 A 4
        stop
    }
}

ACTOR PushLaserJumpage
{
    Radius 1
    Height 1
    Speed 0
    +NOCLIP
    +FORCERADIUSDMG
    -SOLID
    DamageType "PushDamage"
    Obituary "%k shouldn't have killed %o with this."
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 1 A_RadiusThrust(5250,64,0)//A_Explode(16,32,1)
        Stop
    }
}

actor LaserHaze
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    Radius 0
    Height 0
    RenderStyle Add
    Scale 0.05
    Alpha 0.1
    States
    {
    Spawn:
        TNT1 A 0
        XLSE CCCCCDDDDDEEEEE 2 bright A_FadeOut(random(2,5)/400.0)
        loop
    }
}

actor LaserHazeBright : LaserHaze { RenderStyle Translucent }

actor LaserHaze2
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    RenderStyle Add
    Scale 0.1
    Alpha 0.2
    States
    {
    Spawn:
        TNT1 A 0
        XLSE AAAAABBBBB 2 bright A_FadeOut(random(1,3)/100.0)
        loop
    }
}

actor LaserHaze2Bright : LaserHaze2 { RenderStyle Translucent }

actor LaserHaze3
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    Radius 0
    Height 0
    RenderStyle Add
    Scale 0.25
    Alpha 0.3
    States
    {
    Spawn:
        TNT1 A 0
        PFE2 DDDDDDDEEEEEEEE 4 bright A_FadeOut(random(2,5)/400.0)
        stop
    }
}

actor LaserHaze3Bright : LaserHaze3 { RenderStyle Translucent }

actor LaserHaze4
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    RenderStyle Add
    Scale 0.4
    Alpha 0.6
    States
    {
    Spawn:
        TNT1 A 0
        PFE2 AAAAABBBBBCCCCC 4 bright A_FadeOut(random(1,3)/100.0)
        stop
    }
}

actor LaserHaze4Bright : LaserHaze4 { RenderStyle Translucent }

actor LaserHaze5 : LaserHaze { Translation "192:207=168:191" }
actor LaserHaze6 : LaserHaze2 { Translation "192:207=168:191" }

actor BulletBlasterTrail
{
    Scale 0.45
    RenderStyle Add
    Alpha 0.9
    +NOINTERACTION
    +FORCEXYBILLBOARD
    +CLIENTSIDEONLY
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Jump(255,"Spawn1","Spawn2","Spawn3","Spawn4","Spawn5","Spawn6")
        TNT1 A 1
        loop
    Spawn1:
        XLSS A 2 BRIGHT
        stop
    Spawn2:
        XLSS B 2 BRIGHT
        stop
    Spawn3:
        XLSS C 2 BRIGHT
        stop
    Spawn4:
        XLSS D 2 BRIGHT
        stop
    Spawn5:
        XLSS E 2 BRIGHT
        stop
    Spawn6:
        XLSS F 2 BRIGHT
        stop
    }
}

actor BulletBlasterTrailBright : BulletBlasterTrail { RenderStyle Translucent }